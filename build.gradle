buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"
    }
}


group 'spring-contrib'
version '1.0'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'


sourceCompatibility = 1.8


repositories {
    mavenCentral()
    maven {
        url 'http://repo.spring.io/milestone'
    }
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.3'

    compile('org.springframework.plugin:spring-plugin-core:1.2.0.RELEASE') {
        exclude module: 'commons-logging'
        exclude module: 'spring-beans'
        exclude module: 'spring-core'
    }
    compile('com.jayway.jsonpath:json-path:2.0.0') {
        exclude module: 'asm'
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

// add javadoc/source jar tasks as artifacts
artifacts {
//    archives sourcesJar, javadocJar
    archives sourcesJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'spring-contrib'
            artifactId 'spring-data-rest-android'
            version '1.0'

            artifact sourcesJar
//            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'spring-data-rest-android'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mustafaguc/spring-data-rest-android.git'
        version {
            name = '1.0'
            desc = 'Spring DATA Rest for Android'
            vcsTag = '1.0'
//            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
